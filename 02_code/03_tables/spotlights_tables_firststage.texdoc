
/* Basics ______________________________________________________________________

	Project: Spotlights Tanzania
	Purpose: Analysis
	Author: dylan groves, dylanwgroves@gmail.com
	Date Created: 2021/08/01
	Date Edited: 2022/01/04
________________________________________________________________________________*/


/* Introduction ________________________________________________________________*/
	
	clear all	
	clear matrix
	clear mata
	set more off
	global c_date = c(current_date)
	tempfile dta_main 
	tempfile dta_vill

/* Run Prelim File _____________________________________________________________	

	do "${code}/pfm_.master/00_setup/pfm_paths_master.do"
	do "${code}/pfm_ne/pfm_ne_prelim.do"
*/

/* Load Data ___________________________________________________________________*/	

	import excel "${spotlights_tables}/spotlights_analysis_${survey}.xlsx", sheet("firststage") firstrow clear
	save `dta_main', replace


/* Sort Data ___________________________________________________________________*/

	/* Individual Data */
	sort pval
	destring 	variable variablelabel coef se pval r2 N ///
				lasso_coef lasso_se lasso_pval ripval lasso_ripval lasso_r2 lasso_N lasso_ctls lasso_ctls_num ///
				treat_mean treat_sd ctl_mean ctl_sd vill_sd min max, replace
	save `dta_main', replace


/* Create Rank _________________________________________________________________*/

	/* Individual Data */
	# d ;
	local sumstat_vars 	
						journo_any
						journo_visit
						journo_heard
						journo_likely 
						;
	#d cr 
						
						

	gen rank = .
	local i = 1
	
	foreach var of local sumstat_vars {
		replace rank = `i' if variable == "`var'"
		local i = `i' + 1
	}		   

	drop if rank == .
	gsort rank


/* Any Labelling Changes _______________________________________________________*/

	replace variablelabel = "Aware Index" if variable == "journo_any"
	replace variablelabel = "Journo Visit" if variable == "journo_visit"
	replace variablelabel = "Heard Report" if variable == "journo_heard"
	replace variablelabel = "Future Report Likely" if variable == "journo_likely"


/* Create variables ____________________________________________________________*/

	count
	global count = `r(N)'
	
	forval i = 1/$count {
	
		/* Drop pre-existing macros */
		macro drop var`i' b`i' se`i' p`i' r2`i' n`i' ctl`i' c`i' sd`i' mn`i' mx`i'

		global var`i' = variablelabel[`i']
		
		/* Normal */
		global b`i' = strofreal(coef[`i'], "%6.3f")
		global se`i' = strofreal(se[`i'], "%6.3f")
		global p`i' = strofreal(ripval[`i'], "%6.3f")
		global r`i' = strofreal(r2[`i'], "%6.2f")
		global n`i' = strofreal(N[`i'], "%6.0fc")
		global ctl`i' = "No"
		
		/* Lasso */
		global b`i'c = strofreal(lasso_coef[`i'], "%6.3f")
		global se`i'c = strofreal(lasso_se[`i'], "%6.3f")
		global p`i'c = strofreal(lasso_ripval[`i'], "%6.3f")
		global r`i'c = strofreal(lasso_r2[`i'], "%6.2f")
		global n`i'c = strofreal(lasso_N[`i'], "%6.0fc")
		global ctl`i'c = strofreal(lasso_ctls_num[`i'], "%6.0fc")
		
		/* Summary stats */
		global c`i' = strofreal(ctl_mean[`i'], "%6.2f")
		global sd`i' = strofreal(vill_sd[`i'], "%6.2f")
		global mn`i' = strofreal(min[`i'], "%6.0f")
		global mx`i' = strofreal(max[`i'], "%6.0f")
	
	}
	
	/* Assign stars and set p-value to <0.001 instead of 0.000 */
	do "${spotlights_code}/01_helpers/spotlights_helper_stars.do"	
	
/* Make Table __________________________________________________________________*/

texdoc init "${spotlights_tables_clean}/spotlights_tables_firststage.tex", replace nolog

texdoc write 	{
texdoc write 	\def\sym#1{\ifmmode^{#1}\else\(^{#1}\)\fi}
texdoc write 	\begin{tabular}{l*{5}{c}}
texdoc write 	\Xhline{2\arrayrulewidth}\addlinespace[3pt]	

texdoc write    &\multicolumn{3}{c}{Awareness} &\multicolumn{1}{c}{Expectations} 	\tstrut \bstrut \\ \cmidrule(r){2-4} \cmidrule(r){5-5} 

texdoc write   	 &\multicolumn{1}{c}{$var1} &\multicolumn{1}{c}{$var2} &\multicolumn{1}{c}{$var3}  	&\multicolumn{1}{c}{$var4} 	\tstrut \bstrut \\ 

texdoc write 	\Xhline{2\arrayrulewidth}

texdoc write    							& (1)				& (2)				& (3)			& (4)				\\

texdoc write 	Treatment      				& $b1\sym{$s1}  	& $b2\sym{$s2} 	& $b3\sym{$s3} 	& $b4\sym{$s4}		\\
texdoc write    Standard Error 				& $se1				& $se2    			& $se3 			& $se4 				\\
texdoc write 	\hline
texdoc write 	RI \$p\$-value 				& $p1				& $p2				& $p3  			& $p4				\\
texdoc write 	Hypothesis					& +					& +					& +				& +					\\
texdoc write 	Control Mean      			& $c1				& $c2				& $c3  			& $c4				\\
texdoc write 	Control SD 					& $sd1				& $sd2				& $sd3 			& $sd4				\\
texdoc write	DV Range					& [$mn1-$mx1]		& [$mn2-$mx2]		& [$mn3-$mx3] 	& [$mn4-$mx4]		\\	
texdoc write 	Blocked FE 					& Yes 				&  Yes   			& Yes			& Yes				\\
texdoc write 	Controls 					& $ctl1 			&  $ctl2   			& $ctl3			& $ctl4				\\
texdoc write 	Adj-\$ R^2\$				& $r1 				& $r2   			& $r3			& $r4				\\
texdoc write 	Observations      			& $n1 				& $n2   			& $n3			& $n4				\\
texdoc write 	\Xhline{2\arrayrulewidth}
texdoc write 	\end{tabular}
texdoc write 	}

texdoc close









